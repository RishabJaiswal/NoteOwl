apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.owl.noteowl"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 3
        versionName "v0.3.0-${getGitHash()}"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".dev"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            //changes the generated .apk name
            if (variant.name.contains("release")) {
                def formattedDate = new Date().format('dd.MM.yy_HHmm')
                outputFileName = "noteOwl_${variant.name}_${variant.versionCode}_${formattedDate}_${getGitHash()}.apk"
            }
        }
    }
}

dependencies {
    def lifecycle_version = "2.0.0"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.core:core-ktx:1.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'com.google.android.material:material:1.1.0-alpha02'
    implementation "com.airbnb.android:lottie:$lottie_version"
    implementation 'com.github.rtugeek:colorseekbar:1.7.3'
    implementation 'com.google.firebase:firebase-core:16.0.1'
    //crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'
    //android viewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    //picasso
    implementation 'com.squareup.picasso:picasso:2.71828'
    //retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    //gson converter
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    //kotlin field names helper
    kapt 'dk.ilios:realmfieldnameshelper:1.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

apply plugin: 'com.google.gms.google-services'